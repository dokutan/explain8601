expression                    =  date-time-expression / set / set-single
<date-time-expression>        =  date / date-e / date-year / time / time-e / date-time / date-time-e / timezone / timezone-e / interval / interval-e / duration / recurring-interval


;; set expressions
set                           =  '{' *(date-time-expression ',') date-time-expression '}'
set-single                    =  '[' *(date-time-expression ',') date-time-expression ']'


;; date
date                          =  calendar-date / week-date / ordinal-date
date-e                        =  calendar-date-e / week-date-e / ordinal-date-e
date-tail                     =  calendar-date-tail / week-date-tail / ordinal-date-tail

; calendar dates
calendar-date                 =  calendar-date-day / calendar-date-month / calendar-date-grouping / calendar-date-year / calendar-date-decade / calendar-date-century
  calendar-date-day           =  (year month day) / (year [qualifier] <'-'> month [qualifier] <'-'> day [qualifier]) / (year-expanded month day) / (year-expanded [qualifier] <'-'> month [qualifier] <'-'> day [qualifier])
  calendar-date-month         =  (year [qualifier] <'-'> month [qualifier]) / (year-expanded [qualifier] <'-'> month [qualifier])
  calendar-date-grouping      =  (year [qualifier] <'-'> grouping [qualifier]) / (year-expanded [qualifier] <'-'> grouping [qualifier])
  calendar-date-year          =  (year [qualifier]) / (year-expanded [qualifier])
  calendar-date-decade        =  (decade [qualifier]) / (decade-expanded [qualifier])
  calendar-date-century       =  (century [qualifier]) / (century-expanded [qualifier])
calendar-date-tail            =  calendar-date-tail-day / calendar-date-tail-month
  calendar-date-tail-day      =  ([month [qualifier] <'-'>] day [qualifier]) / ([month] day)
  calendar-date-tail-month    =  month [qualifier]

; calendar dates (explicit form)
calendar-date-e               =  calendar-date-day-e / calendar-date-month-e / calendar-date-grouping-e / calendar-date-year-e / calendar-date-decade-e / calendar-date-century-e
  calendar-date-day-e         =  year-e [qualifier] month-e [qualifier] day-e [qualifier] [timezone-e]
  calendar-date-month-e       =  year-e [qualifier] month-e [qualifier] [timezone-e]
  calendar-date-grouping-e    =  year-e [qualifier] grouping-e [qualifier] [timezone-e]
  calendar-date-year-e        =  year-e [qualifier] [timezone-e]
  calendar-date-decade-e      =  decade-e [qualifier] [timezone-e]
  calendar-date-century-e     =  century-e [qualifier] [timezone-e]
calendar-date-tail-e          =  calendar-date-tail-day-e / calendar-date-tail-month-e
  calendar-date-tail-day-e    =  [month-e [qualifier]] day-e [qualifier] [timezone-e]
  calendar-date-tail-month-e  =  month-e [qualifier] [timezone-e]


; week dates
week-date                     =  week-date-day / week-date-week
  week-date-day               =  (year week weekday) / (year [qualifier] <'-'> week [qualifier] <'-'> weekday [qualifier]) / (year-expanded week weekday) / (year-expanded [qualifier] <'-'> week [qualifier] <'-'> weekday [qualifier])
  week-date-week              =  (year [[qualifier] <'-'>] week [qualifier]) / (year-expanded [[qualifier] <'-'>] week [qualifier])
week-date-tail                =  week-date-tail-day / week-date-tail-week
  week-date-tail-day          =  ([week [qualifier] <'-'>] weekday [qualifier]) / ([week] weekday)
  week-date-tail-week         =  week [qualifier]

; week dates (explicit form)
week-date-e                   =  week-date-day-e / week-date-week-e
  week-date-day-e             =  year-e [qualifier] week-e [qualifier] weekday-e [qualifier] [timezone-e]
  week-date-week-e            =  year-e [qualifier] week-e [qualifier] [timezone-e]
week-date-tail-e              =  week-date-tail-day-e / week-date-tail-week-e
  week-date-tail-day-e        =  [week-e [qualifier]] weekday-e [qualifier] [timezone-e]
  week-date-tail-week-e       =  week-e [qualifier] [timezone-e]


; ordinal dates
ordinal-date                  =  (year [[qualifier] <'-'>] day-of-year) / (year-expanded [[qualifier] <'-'>] day-of-year)
ordinal-date-tail             =  day-of-year [qualifier]

; ordinal dates (explicit form)
ordinal-date-e                =  year-e [qualifier] day-of-year-e [qualifier] [timezone-e]
ordinal-date-tail-e           =  day-of-year-e [qualifier] [timezone-e]


; special year forms
date-year                     =  4 DIGIT [significant-digits]
date-year                     =/ <'Y'> [minus] 5* DIGITX [significant-digits]
date-year                     =/ <'Y'> [minus] 1* DIGIT exponent [significant-digits]
exponent                      =  <'E'> 1* DIGIT
significant-digits            =  <'S'> 1* DIGIT


;; time
time                          =  time-second / time-minute / time-hour
   time-second                =  [<'T'>] normal-time-second [fraction] [timezone]
   time-minute                =  [<'T'>] normal-time-minute [fraction] [timezone]
   time-hour                  =  [<'T'>] hour [fraction] [timezone]
time-tail                     =  time-tail-minute / time-tail-second
  <time-tail-minute>          =  normal-time-tail-minute [fraction] [timezone]
  <time-tail-second>          =  normal-time-tail-second [fraction] [timezone]

<normal-time>                 =  normal-time-second / normal-time-minute / hour
<normal-time-second>          =  (hour [qualifier] <':'> minute [qualifier] <':'> second [qualifier]) / (hour minute second)
<normal-time-minute>          =  (hour [qualifier] <':'> minute [qualifier]) / (hour minute)

<normal-time-tail-second>     =  ([hour [qualifier] <':'> [minute [qualifier] <':'>]] second [qualifier]) / ([hour [minute]] second)
<normal-time-tail-minute>     =  ([hour [qualifier] <':'>] minute) / (hour minute)

;; time (explicit form)
time-e                        =  time-second-e / time-minute-e / time-hour-e
  <time-second-e>             =  [<'T'>] [hour-e [qualifier]] [minute-e [qualifier]] (second-e / second-e-fraction) [qualifier] [timezone-e]
  <time-minute-e>             =  [<'T'>] [hour-e [qualifier]] (minute-e / minute-e-fraction) [qualifier] [timezone-e]
  <time-hour-e>               =  [<'T'>] (hour-e / hour-e-fraction) [qualifier] [timezone-e]
  <time-tail-hour-e>          =  time-hour-e
  <time-tail-minute-e>        =  time-minute-e
  <time-tail-second-e>        =  time-second-e


;; time zone designator
timezone                      =  'Z' / (pm hour [[<':'>] minute])

;; time zone designator (explicit form)
timezone-e                    =  'Z' [[minus] hour-e / ([hour-e] minute-e) / ([hour-e] [minute-e] second-e)]


;; date + time
date-time                     =  calendar-date-time / week-date-time / ordinal-date-time
calendar-date-time            =  calendar-date-day <'T'> time
week-date-time                =  week-date-day <'T'> time
ordinal-date-time             =  ordinal-date <'T'> time

;; date + time (explicit form)
date-time-e                   =  calendar-date-time-e / week-date-time-e / ordinal-date-time-e
calendar-date-time-e          =  calendar-date-day-e time-e
week-date-time-e              =  week-date-day-e time-e
ordinal-date-time-e           =  ordinal-date-e time-e


;; duration
duration                      =  duration-complete / duration-alternative / duration-precedence
duration                      =/ [minus] <'P'> [years <'Y'>] [months <'M'>] [weeks <'W'>] [days <'D'>] [<'T'> [hours <'H'>] [minutes <'M'>] [seconds <'S'>]]
  <duration-complete>         =  [minus] <'P'> years <'Y'> months <'M'> days <'D'> <'T'> hours <'H'> minutes <'M'> seconds <'S'>
  <duration-alternative>      =  [minus] <'P'> calendar-date
  <duration-alternative>      =/ [minus] <'P'> calendar-date-day <'T'> normal-time
  <duration-alternative>      =/ [minus] <'P'> ordinal-date [<'T'> normal-time]
  <duration-precedence>       =  [minus] 2* duration-component
  <duration-component>        =  <'P'> ([years <'Y'>] / [months <'M'>] / [weeks <'W'>] / [days <'D'>] / [<'T'> hours <'H'>] / [<'T'> minutes <'M'>]  [<'T'> seconds <'S'>])


;; interval
interval                      =  (date / date-time) solidus duration
interval                      =/ duration solidus (date / date-time)

interval                      =/ calendar-date-century solidus calendar-date-century
interval                      =/ calendar-date-decade solidus calendar-date-decade
interval                      =/ calendar-date-year solidus calendar-date-year
interval                      =/ calendar-date-month solidus (calendar-date-month / calendar-date-tail-month)
interval                      =/ calendar-date-day solidus (calendar-date-day / calendar-date-tail-day)
interval                      =/ week-date-week solidus (week-date-week / week-date-tail-week)
interval                      =/ week-date-day solidus (week-date-day / week-date-tail-day)
interval                      =/ ordinal-date solidus (ordinal-date / ordinal-date-tail)
interval                      =/ calendar-date-day <'T'> time-second solidus (time-tail-second / ((calendar-date-day / calendar-date-tail-day) <'T'> time-second))
interval                      =/ week-date-day <'T'> time-second solidus (time-tail-second / ((week-date-day / week-date-tail-day) <'T'> time-second))
interval                      =/ ordinal-date <'T'> time-second solidus (time-tail-second / ((ordinal-date / ordinal-date-tail) <'T'> time-second))
interval                      =/ calendar-date-day <'T'> time-minute solidus (time-tail-minute / ((calendar-date-day / calendar-date-tail-day) <'T'> time-minute))
interval                      =/ week-date-day <'T'> time-minute solidus (time-tail-minute / ((week-date-day / week-date-tail-day) <'T'> time-minute))
interval                      =/ ordinal-date <'T'> time-minute solidus (time-tail-minute / ((ordinal-date / ordinal-date-tail) <'T'> time-minute))
interval                      =/ calendar-date-day <'T'> time-hour solidus (hour / ((calendar-date-day / calendar-date-tail-day) <'T'> time-hour))
interval                      =/ week-date-day <'T'> time-hour solidus (hour / ((week-date-day / week-date-tail-day) <'T'> time-hour))
interval                      =/ ordinal-date <'T'> time-hour solidus (hour / ((ordinal-date / ordinal-date-tail) <'T'> time-hour))

interval                      =/ calendar-date-century solidus interval-limit
interval                      =/ calendar-date-decade solidus interval-limit
interval                      =/ calendar-date-year solidus interval-limit
interval                      =/ calendar-date-month solidus interval-limit
interval                      =/ calendar-date-day solidus interval-limit
interval                      =/ week-date-week solidus interval-limit
interval                      =/ week-date-day solidus interval-limit
interval                      =/ ordinal-date solidus interval-limit
interval                      =/ calendar-date-day <'T'> time solidus interval-limit
interval                      =/ week-date-day <'T'> time solidus interval-limit
interval                      =/ ordinal-date <'T'> time solidus interval-limit

interval                      =/ interval-limit solidus calendar-date-century
interval                      =/ interval-limit solidus calendar-date-decade
interval                      =/ interval-limit solidus calendar-date-year
interval                      =/ interval-limit solidus calendar-date-month
interval                      =/ interval-limit solidus calendar-date-day
interval                      =/ interval-limit solidus week-date-week
interval                      =/ interval-limit solidus week-date-day
interval                      =/ interval-limit solidus ordinal-date
interval                      =/ interval-limit solidus calendar-date-day 'T' time
interval                      =/ interval-limit solidus week-date-day 'T' time
interval                      =/ interval-limit solidus ordinal-date 'T' time

;; interval (explicit form)
interval-e                    =  (date-e / date-time-e) solidus duration
interval-e                    =/ duration solidus (date-e / date-time-e)

interval-e                    =/ calendar-date-century-e solidus calendar-date-century-e
interval-e                    =/ calendar-date-decade-e solidus calendar-date-decade-e
interval-e                    =/ calendar-date-year-e solidus calendar-date-year-e
interval-e                    =/ calendar-date-month-e solidus (calendar-date-month-e / calendar-date-tail-month-e)
interval-e                    =/ calendar-date-day-e solidus (calendar-date-day-e / calendar-date-tail-day-e)
interval-e                    =/ week-date-week-e solidus (week-date-week-e / week-date-tail-week-e)
interval-e                    =/ week-date-day-e solidus (week-date-day-e / week-date-tail-day-e)
interval-e                    =/ ordinal-date-e solidus (ordinal-date-e / ordinal-date-tail-e)
interval-e                    =/ calendar-date-day-e <'T'> time-second-e solidus (time-tail-second-e / ((calendar-date-day-e / calendar-date-tail-day-e) <'T'> time-second-e))
interval-e                    =/ week-date-day-e <'T'> time-second-e solidus (time-tail-second-e / ((week-date-day-e / week-date-tail-day-e) <'T'> time-second-e))
interval-e                    =/ ordinal-date-e <'T'> time-second-e solidus (time-tail-second-e / ((ordinal-date-e / ordinal-date-tail-e) <'T'> time-second-e))
interval-e                    =/ calendar-date-day-e <'T'> time-minute-e solidus (time-tail-minute-e / ((calendar-date-day-e / calendar-date-tail-day-e) <'T'> time-minute-e))
interval-e                    =/ week-date-day-e <'T'> time-minute-e solidus (time-tail-minute-e / ((week-date-day-e / week-date-tail-day-e) <'T'> time-minute-e))
interval-e                    =/ ordinal-date-e <'T'> time-minute-e solidus (time-tail-minute-e / ((ordinal-date-e / ordinal-date-tail-e) <'T'> time-minute-e))
interval-e                    =/ calendar-date-day-e <'T'> time-hour-e solidus (time-tail-hour-e / ((calendar-date-day-e / calendar-date-tail-day-e) <'T'> time-hour-e))
interval-e                    =/ week-date-day-e <'T'> time-hour-e solidus (time-tail-hour-e / ((week-date-day-e / week-date-tail-day-e) <'T'> time-hour-e))
interval-e                    =/ ordinal-date-e <'T'> time-hour-e solidus (time-tail-hour-e / ((ordinal-date-e / ordinal-date-tail-e) <'T'> time-hour-e))

interval-e                    =/ calendar-date-century-e solidus interval-limit
interval-e                    =/ calendar-date-decade-e solidus interval-limit
interval-e                    =/ calendar-date-year-e solidus interval-limit
interval-e                    =/ calendar-date-month-e solidus interval-limit
interval-e                    =/ calendar-date-day-e solidus interval-limit
interval-e                    =/ week-date-week-e solidus interval-limit
interval-e                    =/ week-date-day-e solidus interval-limit
interval-e                    =/ ordinal-date-e solidus interval-limit
interval-e                    =/ calendar-date-day-e <'T'> time-e solidus interval-limit
interval-e                    =/ week-date-day-e <'T'> time-e solidus interval-limit
interval-e                    =/ ordinal-date-e <'T'> time-e solidus interval-limit

interval-e                    =/ interval-limit solidus calendar-date-century-e
interval-e                    =/ interval-limit solidus calendar-date-decade-e
interval-e                    =/ interval-limit solidus calendar-date-year-e
interval-e                    =/ interval-limit solidus calendar-date-month-e
interval-e                    =/ interval-limit solidus calendar-date-day-e
interval-e                    =/ interval-limit solidus week-date-week-e
interval-e                    =/ interval-limit solidus week-date-day-e
interval-e                    =/ interval-limit solidus ordinal-date-e
interval-e                    =/ interval-limit solidus calendar-date-day-e 'T' time-e
interval-e                    =/ interval-limit solidus week-date-day-e 'T' time-e
interval-e                    =/ interval-limit solidus ordinal-date-e 'T' time-e


;; recurring interval
recurring-interval            =  <'R'> repetitions <solidus> interval
recurring-interval            =/ <'R'> repetitions <solidus> interval-e
repetitions                   =  '-1' / * DIGIT



century                       =  [qualifier] 2 DIGITX
decade                        =  [qualifier] 3 DIGITX
year                          =  [qualifier] 4 DIGITX
month                         =  [qualifier] 2 DIGITX
week                          =  [qualifier] <'W'> 2 DIGITX ; actually 00 - 53
weekday                       =  [qualifier] ('1' / '2' / '3' / '4' / '5' / '6' / '7' / 'X')
day-of-year                   =  [qualifier] 3 DIGITX ; actually 001 - 366
day                           =  [qualifier] 2 DIGITX
hour                          =  [qualifier] 2 DIGITX ; actually 00 - 23
minute                        =  [qualifier] 2 DIGITX ; actually 00 - 59
second                        =  [qualifier] 2 DIGITX ; actually 00 - 60
grouping                      =  [qualifier] grouping-value

century-expanded              =  [qualifier] pm 2* DIGITX ; assuming expansion by 0 digits
decade-expanded               =  [qualifier] pm 3* DIGITX ; assuming expansion by 0 digits
year-expanded                 =  [qualifier] pm 4* DIGITX ; assuming expansion by 0 digits

century-e                     =  [minus] (1* DIGITX / unspecified-value) [qualifier] <'C'>
decade-e                      =  [minus] (1* DIGITX / unspecified-value) [qualifier] <'J'>
year-e                        =  [minus] (1* DIGITX / unspecified-value) [qualifier] <'Y'>
month-e                       =  (1* DIGITX / unspecified-value) [qualifier] <'M'>
week-e                        =  (1* DIGITX / unspecified-value) [qualifier] <'W'>
weekday-e                     =  ((* '0' ('1' / '2' / '3' / '4' / '5' / '6' / '7' / 'X')) / unspecified-value) [qualifier] <'K'>
day-of-year-e                 =  (1* DIGITX / unspecified-value) [qualifier] <'O'>
day-e                         =  (1* DIGITX / unspecified-value) [qualifier] <'D'>
hour-e                        =  (1* DIGITX / unspecified-value) [qualifier] <'H'>
minute-e                      =  (1* DIGITX / unspecified-value) [qualifier] <'M'>
second-e                      =  (1* DIGITX / unspecified-value) [qualifier] <'S'>
hour-e-fraction               =  (1* DIGITX / unspecified-value) fraction [qualifier] <'H'>
minute-e-fraction             =  (1* DIGITX / unspecified-value) fraction [qualifier] <'M'>
second-e-fraction             =  (1* DIGITX / unspecified-value) fraction [qualifier] <'S'>
grouping-e                    =  * '0' grouping-value [qualifier] <'A'>

DIGITX                        =  '0' / '1' / '2' / '3' / '4' / '5' / '6' / '7' / '8' / '9' / 'X'
<grouping-value>              =  '21' / '22' / '23' / '24' / '25' / '26' / '27' / '28' / '29' / '30'
<grouping-value>              =/ '31' / '32' / '33' / '34' / '35' / '36' / '37' / '38' / '39' / '40' / '41'
years                         =  [minus] 1* DIGIT [fraction]
months                        =  [minus] 1* DIGIT [fraction]
weeks                         =  [minus] 1* DIGIT [fraction]
days                          =  [minus] 1* DIGIT [fraction]
hours                         =  [minus] 1* DIGIT [fraction]
minutes                       =  [minus] 1* DIGIT [fraction]
seconds                       =  [minus] 1* DIGIT [fraction]
<fraction>                    =  ('.' / ',') 1* DIGIT
pm                            =  '+' / '-' / %x2212
minus                         =  '-' / %x2212
solidus                       =  '/' / '--'
<interval-limit>              =  interval-limit-open / interval-limit-unknown
interval-limit-open           =  '..'
interval-limit-unknown        =  ''
qualifier                     =  '~' / '?' / '%'
unspecified-value             =  'X*'
