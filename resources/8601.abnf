representation                =  date / time / date-time / timezone / interval / duration / repeating-interval


;; date
date                          =  calendar-date / week-date / ordinal-date
date-tail                     =  calendar-date-tail / week-date-tail / ordinal-date-tail

; calendar dates
calendar-date                 =  calendar-date-day / calendar-date-month / calendar-date-grouping / calendar-date-year / calendar-date-decade / calendar-date-century
  <calendar-date-day>         =  (year month day) / (year '-' month '-' day) / (pm year-expanded month day) / (pm year-expanded '-' month '-' day)
  <calendar-date-month>       =  (year '-' month) / (pm year-expanded '-' month)
  <calendar-date-grouping>    =  (year '-' grouping) / (pm year-expanded '-' grouping)
  <calendar-date-year>        =  year / (pm year-expanded)
  <calendar-date-decade>      =  decade / (pm decade-expanded)
  <calendar-date-century>     =  century / (pm century-expanded)
calendar-date-tail            =  calendar-date-tail-day / calendar-date-tail-month
  calendar-date-tail-day      =  [month ['-']] day
  calendar-date-tail-month    =  month

; week dates
week-date                     =  week-date-day / week-date-week
  <week-date-day>             =  (year 'W' week weekday) / (year '-' 'W' week '-' weekday) / (pm year-expanded 'W' week weekday) / (pm year-expanded '-' 'W' week '-' weekday)
  <week-date-week>            =  (year ['-'] 'W' week) / (pm year-expanded ['-'] 'W' week)
week-date-tail                =  week-date-tail-day / week-date-tail-week
  week-date-tail-day          =  ['W' week ['-']] weekday
  week-date-tail-week         =  'W' week

; ordinal dates
ordinal-date                  =  (year ['-'] day-of-year) / (pm year-expanded ['-'] day-of-year)
ordinal-date-tail             =  day-of-year


;; time
time                          =  time-second / time-minute / time-hour
  <time-second>               =  ['T'] normal-time-second [fraction] [timezone]
  <time-minute>               =  ['T'] normal-time-minute [fraction] [timezone]
  <time-hour>                 =  ['T'] hour [fraction] [timezone]
time-tail                     =  time-tail-second / time-tail-minute
  <time-tail-second>          =  normal-time-tail-second [fraction] [timezone]
  <time-tail-minute>          =  normal-time-tail-minute [fraction] [timezone]

<normal-time>                 =  normal-time-second / normal-time-minute / hour
<normal-time-second>          =  (hour minute second) / (hour ':' minute ':' second)
<normal-time-minute>          =  hour [':'] minute

<normal-time-tail-second>   =  [hour [':'] [minute [':']]] second
<normal-time-tail-minute>   =  [hour [':']] minute


;; time zone designator
timezone                      =  'Z' / (pm hour [[':'] minute])


;; date + time
date-time                     =  date-day 'T' time
  <date-day>                  =  calendar-date-day / week-date-day / ordinal-date


;; duration
duration                      =  duration-complete / duration-alternative
duration                      =/ 'P' [years 'Y'] [months 'M'] [days 'D'] ['T'  [hours 'H']  [minutes 'M']  [seconds 'S']]
  <duration-complete>         =  'P' years 'Y' months 'M' days 'D' 'T' hours 'H' minutes 'M' seconds 'S'
  <duration-complete>         =/ 'P' weeks 'W'
  <duration-alternative>      =  'P' calendar-date
  <duration-alternative>      =/ 'P' calendar-date-day 'T' normal-time
  <duration-alternative>      =/ 'P' ordinal-date ['T' normal-time]


;; interval
interval                      =  (date / date-time) solidus duration
interval                      =/ duration solidus (date / date-time)
interval                      =/ calendar-date-century solidus calendar-date-century
interval                      =/ calendar-date-decade solidus calendar-date-decade
interval                      =/ calendar-date-year solidus calendar-date-year
interval                      =/ calendar-date-month solidus (calendar-date-month / calendar-date-tail-month)
interval                      =/ calendar-date-day solidus (calendar-date-day / calendar-date-tail-day)
interval                      =/ week-date-week solidus (week-date-week / week-date-tail-week)
interval                      =/ week-date-day solidus (week-date-day / week-date-tail-day)
interval                      =/ ordinal-date solidus (ordinal-date / ordinal-date-tail)
interval                      =/ calendar-date-day 'T' time-second solidus (time-tail-second / ((calendar-date-day / calendar-date-tail-day) 'T' time-second))
interval                      =/ week-date-day 'T' time-second solidus (time-tail-second / ((week-date-day / week-date-tail-day) 'T' time-second))
interval                      =/ ordinal-date 'T' time-second solidus (time-tail-second / ((ordinal-date / ordinal-date-tail) 'T' time-second))
interval                      =/ calendar-date-day 'T' time-minute solidus (time-tail-minute / ((calendar-date-day / calendar-date-tail-day) 'T' time-minute))
interval                      =/ week-date-day 'T' time-minute solidus (time-tail-minute / ((week-date-day / week-date-tail-day) 'T' time-minute))
interval                      =/ ordinal-date 'T' time-minute solidus (time-tail-minute / ((ordinal-date / ordinal-date-tail) 'T' time-minute))
interval                      =/ calendar-date-day 'T' time-hour solidus (hour / ((calendar-date-day / calendar-date-tail-day) 'T' time-hour))
interval                      =/ week-date-day 'T' time-hour solidus (hour / ((week-date-day / week-date-tail-day) 'T' time-hour))
interval                      =/ ordinal-date 'T' time-hour solidus (hour / ((ordinal-date / ordinal-date-tail) 'T' time-hour))


;; repeating interval
repeating-interval            =  'R' repetitions solidus interval
repetitions                   =  '-1' / * DIGIT



year                          =  4 DIGIT
month                         =  '01' / '02' / '03' / '04' / '05' / '06' / '07' / '08' / '09' / '10' / '11' / '12'
day                           =  2 DIGIT
decade                        =  3 DIGIT
century                       =  2 DIGIT
year-expanded                 =  4* DIGIT ; assuming expansion by 0 digits
decade-expanded               =  3* DIGIT ; assuming expansion by 0 digits
century-expanded              =  2* DIGIT ; assuming expansion by 0 digits
day-of-year                   =  3 DIGIT ; actually 001 - 366
week                          =  2 DIGIT ; actually 01 - 53
weekday                       =  '1' / '2' / '3' / '4' / '5' / '6' / '7'
hour                          =  2 DIGIT ; actually 00 - 23
minute                        =  2 DIGIT ; actually 00 - 59
second                        =  2 DIGIT ; actually 00 - 60
years                         =  1* DIGIT
months                        =  1* DIGIT
weeks                         =  1* DIGIT
days                          =  1* DIGIT
hours                         =  1* DIGIT
minutes                       =  1* DIGIT
seconds                       =  1* DIGIT
fraction                      =  ('.' / ',') 1* DIGIT
pm                            =  '+' / '-' / %x2212
solidus                       =  '/' / '--'
grouping                      =  '21' / '22' / '23' / '24' / '25' / '26' / '27' / '28' / '29' / '30'
grouping                      =/ '31' / '32' / '33' / '34' / '35' / '36' / '37' / '38' / '39' / '40' / '41'
